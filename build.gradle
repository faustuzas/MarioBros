buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = 'Mario Bros'
        gdxVersion = '1.6.4'
        roboVMVersion = '1.5.0'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.6.0'
        aiVersion = '1.5.0'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"
    apply plugin: 'antlr'

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        antlr "org.antlr:antlr4:4.3"
    }
}

project(":core") {
    apply plugin: "java"
    apply plugin: 'antlr'
    apply plugin: 'idea'

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        antlr "org.antlr:antlr4:4.3"
    }

    generateGrammarSource {
        arguments = ['-visitor', '-package', 'com.faustas.mariobros.generated']
        outputDirectory = file("${project.buildDir}/generated-src/main/java/")
    }

    generateGrammarSource << {
        copy {
            from "${project.buildDir}/generated-src/main/java/"
            into "src/com/faustas/mariobros/generated/"
        }
    }

    compileJava.dependsOn generateGrammarSource
    compileJava {
        source = ["${project.buildDir}/generated-src/main/java", 'src/main/java']
    }

    // when cleaning, remove the generated sources too
    task removeGeneratedSource(type: Delete) {
        delete 'src/generated/'
    }
    clean.dependsOn removeGeneratedSource

    // mark the generated source as such for IntelliJ IDEA
    sourceSets.main.java.srcDirs += file("src/com/faustas/mariobros/generated/")
    idea {
        module {
            generatedSourceDirs += file('src/com/faustas/mariobros/generated/')
        }
    }
}

tasks.eclipse.doLast {
    delete ".project"
}